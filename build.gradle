plugins {
    id 'fabric-loom' version '1.3-SNAPSHOT' apply false
    id "org.jetbrains.kotlin.jvm" version "1.9.0" apply false
    id "com.modrinth.minotaur" version "2.+" apply false
}

version = "1.0.0"
group = "junk"

subprojects {
    apply plugin: 'fabric-loom'
    apply plugin: 'maven-publish'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'com.modrinth.minotaur'

    version = project.mod_version
    group = project.maven_group


    base {
        archivesName = "${project.archives_base_name}-mc-${project.name}"
    }

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
        maven { url = "https://jitpack.io" }
    }

    loom {
        splitEnvironmentSourceSets()

        mods {
            "cool-resource-finder" {
                sourceSet sourceSets.main
                sourceSet sourceSets.client
            }
        }

        runs {
            // This adds a new gradle task that runs the datagen API: "gradlew runDatagen"
            datagen {
                inherit server
                name "Data Generation"
                vmArg "-Dfabric-api.datagen"
                vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated-${project.name}")}"
                vmArg "-Dfabric-api.datagen.modid=cool-resource-finder"

                runDir "build/datagen"
            }
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 17
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = 17
        }
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    jar {
        from("LICENSE") {
            rename { "${it}_${project.base.archivesName.get()}" }
        }
    }

    processResources {
        inputs.property "version", project.version

        filesMatching("fabric.mod.json") {
            expand "version": project.version
        }
    }

    sourceSets {
        main {
            resources {
                srcDirs += [
                        "src/main/generated-${project.name}"
                ]
            }
        }
    }

    def sourcesRoot = "${rootProject.projectDir}/common/${project.name}"

    sourceSets {
        main {
            java {
                srcDirs += file("${sourcesRoot}/main/java")
            }
            kotlin {
                srcDirs += file("${sourcesRoot}/main/kotlin")
            }
            resources {
                srcDirs += file("${sourcesRoot}/main/resources")
            }
        }
        client {
            java {
                srcDirs += file("${sourcesRoot}/client/java")
            }
            kotlin {
                srcDirs += file("${sourcesRoot}/client/kotlin")
            }
            resources {
                srcDirs += file("${sourcesRoot}/client/resources")
            }
        }

        /**
         * hack incremental source code build, this will add every java or kt file as a regular input
         * from common src folder (the one that source for module-specific symlink) to ensure gradle can detect their
         * timestamp changes and mark job as stale
         * TODO make same thing for resource files(processResources and jar jobs?)
         */
        tasks.compileKotlin {
            def sourceFiles = fileTree(dir: "${rootProject.projectDir}/common/src", include: '**/*.kt')
            inputs.files(sourceFiles)
        }
        tasks.compileJava {
            def sourceFiles = fileTree(dir: "${rootProject.projectDir}/common/src", include: '**/*.java')
            inputs.files(sourceFiles)
        }
    }

    // configure the maven publication
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }

        // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
        repositories {
            // Add repositories to publish to here.
            // Notice: This block does NOT have the same function as the block in the top level.
            // The repositories here will be used for publishing your artifact, not for
            // retrieving dependencies.
        }
    }
    dependencies {
        // To change the versions see the gradle.properties file
        minecraft "com.mojang:minecraft:${project.minecraft_version}"
        mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
        modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

        // Fabric API. This is technically optional, but you probably want it anyway.
        modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
        modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"
    }
    modrinth {
        token = System.getenv("MODRINTH_TOKEN")
        projectId = "eKgd4diD"
        versionNumber = project.mod_version
        versionType = "alpha"
        uploadFile = remapJar
        gameVersions = [project.minecraft_version]
        dependencies {
            required.project "fabric-api"
            required.project "fabric-language-kotlin"
        }
    }
}